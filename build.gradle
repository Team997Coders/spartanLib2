plugins {
	id "java"
	id "edu.wpi.first.GradleRIO" version "2023.1.1-beta-7"
	id 'com.diffplug.spotless' version '6.11.0'
	id 'maven-publish'
	id 'signing'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

//def includeDesktopSupport = false

dependencies {
	implementation wpi.java.deps.wpilib()
	implementation wpi.java.vendor.java()
	implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.6.1'

	testImplementation 'junit:junit:4.13.2'
}

test {
	testLogging.showStandardStreams = true
}

spotless {
	format 'misc', {
		target fileTree(rootDir) {
			include '*.gradle', '*.md', '.gitignore'
			exclude 'LICENSE.md'
		}

		trimTrailingWhitespace()
		indentWithTabs()
		endWithNewline()
	}
	java {
		googleJavaFormat('1.15.0').aosp().reflowLongStrings()
		formatAnnotations()

		licenseHeaderFile 'licenseHeader.txt'
	}
}
compileJava.dependsOn spotlessApply

javadoc {
	if(JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}

tasks.register('updateGitHooks', Copy) {
	from new File(rootProject.rootDir, 'scripts/pre-commit')
	into { new File(rootProject.rootDir, '.git/hooks') }
	fileMode 0775
}
compileJava.dependsOn updateGitHooks

tasks.register('sourceJar', Jar) {
	classifier "sources"
	from sourceSets.main.allJava
}

tasks.register('javadocJar', Jar) {
	classifier "javadoc"
	from javadoc.destinationDir
}.configure {
	mustRunAfter javadoc
}

publishToMavenLocal.dependsOn build

artifacts {
	archives jar
	archives sourceJar
	archives javadocJar
}

repositories {
	mavenLocal()
	mavenCentral()
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId 'org.chsrobotics.lib'
			artifactId 'spartanLib2'
			version '1.2.0'

			from components.java

			pom {
				name = 'spartanLib2'
				description = 'FRC library for team 997. Provides common reusable components for interfacing with WPILib.'
				url = 'https://github.com/Team997Coders/spartanLib2'

				licenses {
					license {
						name = 'GPL-v3.0'
						url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
					}
				}

				organization {
					name = 'Team997Coders'
					url = 'https://www.chsrobotics.org'
				}

				developers {
					developer {
						id = 'TandeJ'
						name = 'Thomas Anderson'
						email = 'tande92834@gmail.com'
					}
					developer {
						id = 'elliotnash'
						name = 'Elliot Nash'
						email = 'elliotnash@gmail.com'
					}
				}

				scm {
					connection = 'scm:git:https://github.com/Team997Coders/spartanLib2.git'
					developerConnection = 'scm:git:https://github.com/Team997Coders/spartanLib2.git'
					url = 'https://github.com/Team997Coders/spartanLib2'
				}
			}

			artifact(sourceJar) {
				classifier = 'sources'
			}
			artifact(javadocJar) {
				classifier = 'javadoc'
			}
		}
	}
	repositories {
		if (project.hasProperty('ossrhUsername')) {
			maven {
				name = "OSSRH"
				setUrl("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
				credentials {
					username = ossrhUsername
					password = ossrhPassword
				}
			}
		}
	}
}

signing {
	sign publishing.publications["maven"]
}
